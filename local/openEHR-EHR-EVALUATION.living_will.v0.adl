archetype (adl_version=1.4; uid=daca8cb4-fea8-4b75-8888-1b0a45f5336d)
	openEHR-EHR-EVALUATION.living_will.v0

concept
	[at0000]

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["date"] = <"2019-10-23">
		["name"] = <"Heidi Koikkalainen">
		["organisation"] = <"NES Digital Service, Edinburgh Napier University">
		["email"] = <"hk.koikkalainen@gmail.com">
	>
	lifecycle_state = <"unmanaged">
	other_contributors = <"Ian McNicoll","VebjÃ¸rn Arntzen">
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record details of a living will document.">
			keywords = <"living, will, advance, directive, decision, legal, statement, future, preference, EoL", ...>
			use = <"Use to record details of a living will document.

Living will is also known as advance directive, advance decision, advance decision to refuse treatment, personal directive, advance healthcare directive, or medical directive. 

A person with capacity may create a living will document to record their wishes for medical care and treatment in advance, which is intended to guide decision-making in future situations in which the person is unable to make or communicate decisions. In some countries, a living will is a legally-binding document, while, in others, it may be legally persuasive without having an official legal status.

A living will is commonly used to refuse life-sustaining treatment which may include, but is not limited to, cardiopulmonary resuscitation (CPR), clinically assisted nutrition and hydration, artificial or mechanical ventilation, and antibiotics for life-threatening infections. 

This archetype has been specifically designed to hold a nested living will details CLUSTER archetype, which will contain specific details of the living will as per national or other local requirements. In countries where a living will has legal status, it MUST be ensured that the EVALUATION.living_will archetype and nested living will details CLUSTER archetype adhere to the specific legal requirements.">
		>
	>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"81b1f4e2a02a9b16afbece6a0799ad73">
		["build_uid"] = <"d42f975a-2e72-3ad4-93f4-a3cf80cb9da4">
	>

definition
	EVALUATION[at0000] matches {    -- Living will
		data matches {
			ITEM_TREE[at0001] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0004] occurrences matches {0..1} matches {    -- Living will status
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0044,    -- Has a living will
									at0045,    -- Does not have a living will
									at0046,    -- Living will retracted
									at0047]    -- Unknown
								}
							}
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0005] occurrences matches {0..1} matches {    -- Living will type
						value matches {
							DV_CODED_TEXT matches {*}
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0006] occurrences matches {0..1} matches {    -- Living will narrative statement
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0007] occurrences matches {0..*} matches {    -- Condition(s) to which the living will applies
						value matches {
							DV_TEXT matches {*}
							DV_CODED_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0052] occurrences matches {0..1} matches {    -- Living will details slot
						include
							archetype_id/value matches {/.*/}
					}
					ELEMENT[at0030] occurrences matches {0..*} matches {    -- Location of the living will document
						value matches {
							DV_TEXT matches {*}
							DV_URI matches {*}
						}
					}
					ELEMENT[at0038] occurrences matches {0..1} matches {    -- Comment
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0055] occurrences matches {0..1} matches {    -- Date last updated
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0010] matches {    -- Item tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0053] occurrences matches {0..1} matches {    -- Start date
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					ELEMENT[at0054] occurrences matches {0..1} matches {    -- End date
						value matches {
							DV_DATE_TIME matches {*}
						}
					}
					allow_archetype CLUSTER[at0025] occurrences matches {0..*} matches {    -- Witness
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.contact_cc\.v0/}
					}
					ELEMENT[at0027] occurrences matches {0..*} matches {    -- Reference to legislation or reason
						value matches {
							DV_TEXT matches {*}
							DV_URI matches {*}
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Living will">
					description = <"A document to communicate the person's wishes with regard to future medical action or end-of-life care, to be used in situations in which the person has lost the capacity to make or express choices.">
				>
				["at0001"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Living will status">
					description = <"The state of the living will.">
				>
				["at0005"] = <
					text = <"Living will type">
					description = <"Description of the living will type.">
					comment = <"Narrative text and/or a list of living will types specialised to local context.">
				>
				["at0006"] = <
					text = <"Living will narrative statement">
					description = <"This field is intended to capture any information that the person wishes to include with regard to their living will in a narrative format.

For example, stating the reasons why the living will has been made, who it has been discussed with, or what is important to the person in relation to their health, care, and quality of life.">
				>
				["at0007"] = <
					text = <"Condition(s) to which the living will applies">
					description = <"The conditions or situations in which the person wishes the living will to apply.

The living will normally applies to the specified condition(s) if the person can no longer make or communicate decisions about their medical treatment and is unlikely to regain the ability to make such decisions.">
				>
				["at0010"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0025"] = <
					text = <"Witness">
					description = <"Personal details of a witness.">
				>
				["at0027"] = <
					text = <"Reference to legislation or reason">
					description = <"Information regarding the law or other relevant guidance (for example, religious reasons/beliefs) that apply to the living will.">
					comment = <"Narrative text and/or a link to a relevant document or information online.">
				>
				["at0030"] = <
					text = <"Location of the living will document">
					description = <"Information regarding where the living will document is stored and how to gain access to it. Can also include a link to the document.">
					comment = <"Narrative text and/or a link to the document.">
				>
				["at0038"] = <
					text = <"Comment">
					description = <"Additional narrative about the living will not captured in other fields.">
				>
				["at0044"] = <
					text = <"Has a living will">
					description = <"The person has a living will.">
				>
				["at0045"] = <
					text = <"Does not have a living will">
					description = <"The person does not have a living will.">
				>
				["at0046"] = <
					text = <"Living will retracted">
					description = <"The person has retracted their living will.">
				>
				["at0047"] = <
					text = <"Unknown">
					description = <"It is not known whether the person has a living will.">
				>
				["at0052"] = <
					text = <"Living will details slot">
					description = <"Slot to include a CLUSTER archetype containing specific details about the living will decision(s) as per national or other local requirements.">
				>
				["at0053"] = <
					text = <"Start date">
					description = <"The date when the living will is valid from.">
				>
				["at0054"] = <
					text = <"End date">
					description = <"The date when the living will is valid until.">
				>
				["at0055"] = <
					text = <"Date last updated">
					description = <"The date when the living will was last updated.">
				>
			>
		>
	>
